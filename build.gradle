import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

plugins {
  id "com.install4j.gradle" version "9.0.2"
  id "com.dorongold.task-tree" version "1.5"
  id "de.undercouch.download" version "4.1.1"
}

//allprojects {
//    gradle.projectsEvaluated {
//        tasks.withType(JavaCompile) {
//            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
//        }
//    }
//}

allprojects {
    apply plugin: "eclipse"
    apply plugin: "idea"

    version = '3.3.0'

    ext {
        appName = "GaiaSky"
        gdxVersion = "1.11.0"
        gdxcontrollersVersion = "2.2.2"
        lwjglVersion = "3.3.1"
        jcommanderVersion = "1.81"
        slf4jVersion = "1.7.+"
        sparkjavaVersion = "2.9.+"
        jafamaVersion = "2.3.+"
        compressVersion = "1.21"
        commonsioVersion = "2.+"
        commonsimagingVersion = "+"
        commonsmathVersion = "3.+"
        py4jVersion = "0.10.9.+"
        oshiVersion = "5.8.+"
        stilVersion = "4.0.+"
        jsampVersion = "1.3.+"
        jsoupVersion = "1.14.+"
        apfloatVersion = "1.10.+"
        jacksonVersion = "2.13.2"
        joiseVersion = "1.+"
        ashleyVersion = "+"
    }

    repositories {
        mavenCentral()
    }
}

project(":core") {
    apply plugin: "java-library"

    def cmd = "git describe --abbrev=0 --tags HEAD"
    def proc = cmd.execute()
    project.ext.tag = proc.text.trim()

    cmd = "git rev-parse --short HEAD"
    proc = cmd.execute()
    project.ext.rev = proc.text.trim()

    if (DefaultNativePlatform.currentOperatingSystem.windows) {
        project.ext.system = DefaultNativePlatform.currentOperatingSystem.getName() + " " + DefaultNativePlatform.currentArchitecture.getName()
    } else {
        cmd = "uname -snmr"
        proc = cmd.execute()
        project.ext.system = proc.text.trim()
    }

    project.ext.builder = System.properties["user.name"]
    project.ext.buildtime = new Date()
    version = "$tag"

    println ""
    println "CURRENT SYSTEM"
    println "=============="
    println "java version: " + JavaVersion.current().toString()
    println "system: $system"

    println ""
    println "GAIA SKY"
    println "========"
    println "git tag: $tag"
    println "git rev: $rev"
    println "buildtime: $buildtime"
    println "builder: $builder"
    println ""

    // Set some build variables
    project.ext.baseDir = System.getProperty("user.dir")
    project.ext.tagRev = project.tag + "." + project.rev
    project.ext.distName = "gaiasky-$tagRev"
    project.ext.releasesDir = "$baseDir/releases"
    project.ext.distDir = "$releasesDir/$distName"
    project.ext.packageName = "packages-$tagRev"
    project.ext.packageDir = "$releasesDir/$packageName"

    println ""
    println "BUILD VARIABLES AND INFO"
    println "========================"
    println "base dir: $baseDir"
    println "tag.rev: $tagRev"
    println "dist name: $distName"
    println "dist dir: $distDir"
    println "packages name: $packageName"
    println "packages dir: $packageDir"
    println ""

    tasks.withType(JavaCompile) { options.compilerArgs << "-parameters" }

    dependencies {
        // *****************************
        // GENERATE SKINS (PackTextures)
        // *****************************
        //api "com.badlogicgames.gdx:gdx-tools:$gdxVersion"

        // ************
        // REGULAR DEPS
        // ************
        api group: "org.lwjgl", name: "lwjgl-openvr", version: "$lwjglVersion"
        api "org.lwjgl:lwjgl-openvr:$lwjglVersion:natives-linux"
        api "org.lwjgl:lwjgl-openvr:$lwjglVersion:natives-windows"
        api "org.lwjgl:lwjgl-openvr:$lwjglVersion:natives-macos"

        api "com.badlogicgames.gdx:gdx:$gdxVersion"
        api "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
        api "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"

        // gdx-controllers:2.+ bases up on SDL 2.0.12
        api "com.badlogicgames.gdx-controllers:gdx-controllers-core:$gdxcontrollersVersion"
        api "com.badlogicgames.gdx-controllers:gdx-controllers-desktop:$gdxcontrollersVersion"

        // Ashley
        api "com.badlogicgames.ashley:ashley:$ashleyVersion"

        // STIL library to load datasets
        api group: "uk.ac.starlink", name: "stil", version: "$stilVersion"
        // JSAMP for SAMP communication
        api group: "uk.ac.starlink", name: "jsamp", version: "$jsampVersion"
        // Apfloat for arbitrary precision floating point numbers
        api group: "org.apfloat", name: "apfloat", version: "$apfloatVersion"

        // File utilities
        api group: "commons-io", name: "commons-io", version: "$commonsioVersion"
        // Compression
        api "org.apache.commons:commons-compress:$compressVersion"
        // Command line arguments
        api "com.beust:jcommander:$jcommanderVersion"
        // Fast math
        api "net.jafama:jafama:$jafamaVersion"

        // Scripting
        api group: "net.sf.py4j", name: "py4j", version: "$py4jVersion"
        // System information
        api group: "com.github.oshi", name: "oshi-core", version: "$oshiVersion"
        // Screenshots and frames
        api group: "org.apache.commons", name: "commons-imaging", version: "$commonsimagingVersion"
        // Interpolators
        api group: "org.apache.commons", name: "commons-math3", version: "$commonsmathVersion"
        // Yaml
        api group: "com.fasterxml.jackson.core", name: "jackson-databind", version: "$jacksonVersion"
        api group: "com.fasterxml.jackson.dataformat", name: "jackson-dataformat-yaml", version: "$jacksonVersion"
        api group: "com.fasterxml.jackson.datatype", name: "jackson-datatype-jsr310", version: "$jacksonVersion"

        // Joise
        api group: "com.sudoplay.joise", name: "joise", version: "$joiseVersion"


        // ****************
        // REST SERVER DEPS
        // ****************
        api "org.slf4j:slf4j-nop:$slf4jVersion"
        api "com.sparkjava:spark-core:$sparkjavaVersion"

        // *************
        // INTERNAL DEPS
        // *************
        api files("../assets")
    }

    jar {
        manifest {
            attributes "Implementation-Title": "Gaia Sky",
            "Implementation-Version": archiveVersion
        }
        from("../assets") {
            include "font/main-font.fnt"
            include "font/main-font.png"
            include "font/font2d.fnt"
            include "font/font2d.png"
            include "font/font-titles.fnt"
            include "font/font-titles.png"
            include "icon/gs_icon.png"
            include "icon/gsvr_icon.png"
            include "icon/gsascii.txt"
            include "img/**"
            include "text/**"
            include "shader/**"
            include "data/**"
            include "shaders/**"
            include "skins/**"
            exclude "skins/raw"
            exclude "assets-bak/**"
        }
        from("build/classes/main") { include "**" }
    }

}

task createWrapper(type: Wrapper) { gradleVersion = "7.3.3" }
